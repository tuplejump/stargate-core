plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.1.1'
group = 'com.tuplejump'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

def luceneVersion = '5.5.0'

dependencies {

    compile('com.clearspring.analytics:stream:2.7.0')

    compile('org.mvel:mvel2:2.2.2.Final')

    //lucene dependencies
    compile group: 'org.apache.lucene', name: 'lucene-core', version: luceneVersion
    compile group: 'org.apache.lucene', name: 'lucene-spatial', version: luceneVersion
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: luceneVersion
    compile("org.apache.lucene:lucene-queryparser:$luceneVersion") {
        exclude group: 'org.apache.lucene', module: 'lucene-sandbox'
    }
    compile("org.apache.lucene:lucene-join:$luceneVersion")
    compile('joda-time:joda-time:2.3')
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.2'

    //provided dependencies - do not copy into dist
    compile('org.codehaus.jackson:jackson-mapper-asl:1.9.2')
    compile('org.codehaus.jackson:jackson-core-asl:1.9.2')

    compile('org.apache.commons:commons-lang3:3.1')
    compile('org.slf4j:slf4j-api:1.7.2')
    compile('org.slf4j:slf4j-log4j12:1.7.2')

    compile('org.apache.cassandra:cassandra-all:2.1.12') {
        exclude group: 'asm'
        exclude group: 'org.apache.mina'
    }

    compile('org.javolution:javolution:5.3.1')

    //testing dependencies
    testCompile group: 'junit', name: 'junit', version: '4.11'

    testCompile('com.datastax.cassandra:cassandra-driver-core:2.1.8') {
        exclude group: 'org.apache.cassandra', module: 'cassandra-all'
        exclude group: 'log4j'
        exclude group: 'io.netty'
        exclude group: 'com.google.guava'
    }

    testCompile('org.cassandraunit:cassandra-unit:2.1.9.2') {
        exclude group: 'org.apache.cassandra', module: 'cassandra-all'
        exclude group: 'log4j'
        exclude group: 'org.sl4j'
        exclude group: 'org.jboss.netty'
    }

    testCompile group: 'net.sourceforge.argo', name: 'argo', version: '3.7'

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


shadowJar {
    dependencies {
        include(dependency('com.clearspring.analytics:stream'))
        include(dependency('org.mvel:mvel2'))
        include(dependency('org.apache.lucene:lucene-core'))
        include(dependency('org.apache.lucene:lucene-grouping'))
        include(dependency('org.apache.lucene:lucene-spatial'))
        include(dependency('org.apache.lucene:lucene-analyzers-common'))
        include(dependency('org.apache.lucene:lucene-queries'))
        include(dependency('org.apache.lucene:lucene-queryparser'))
        include(dependency('org.apache.lucene:lucene-join'))
        include(dependency('joda-time:joda-time'))
        include(dependency('commons-collections:commons-collections'))
        include(dependency('org.javolution:javolution'))
    }
    relocate 'com.clearspring.analytics', 'shaded.com.clearspring.analytics'
}

task copyJarToCCM(dependsOn: shadowJar) << {
    if (project.hasProperty('cassandraVersion')) {
        copy {
            from('build/libs') {
              include('*-all.jar')
            }
            into System.getProperty('user.home') + '/.ccm/repository/' + project.property('cassandraVersion') + '/lib'
        }
    } else {
        throw new RuntimeException("Please set cassandraVersion before running this")
    }
}


task copySamples(type: Copy) {
    from 'src/test/resources/samples'
    into 'build/dist/stargate/samples'
}

task copyJar(type: Copy) {
    dependsOn([shadowJar, copySamples])
    from ('build/libs'){
        include('*-all.jar')
    }
    into 'build/dist/stargate/libs'
}


task dist(type: Tar) {
    baseName 'stargate-core'
    extension 'tar.gz'
    compression 'GZIP'
    dependsOn copyJar
    from('build/dist')
}

install {
    repositories.mavenInstaller {
        pom.version = version
        pom.groupId = group
        pom.artifactId = 'stargate-core'
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            pom.artifactId = 'stargate-core'

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'stargate core'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Lucene based secondary indices for Cassandra'
                url 'http://tuplejump.github.io/stargate/'

                scm {
                    connection 'scm:git:git@github.com:tuplejump/stargate-core.git'
                    developerConnection 'scm:git:git@github.com:tuplejump/stargate-core.git'
                    url 'https://github.com/tuplejump/stargate-core'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'trulite'
                        name 'trulite'
                        email 'satya@tuplejump.com'
                    }
                }
            }
        }
    }
}
